# -*- shell-script -*-
#*****************************************************************************
#* 
#*  @file          comgt_gprs.scr
#*
#*  comgt script file to talk to GPRS terminal adapters
#*  (tested Siemens MC35i)
#*
#*  @version       1.0 (\$Revision$)
#*  @author        Guido Classen <br>
#*                 Weiss-Electronic GmbH
#* 
#*  $LastChangedBy$  
#*  $Date$
#*  $URL$
#*
#*  @par Modification History:
#*    2007-12-03 gc: initial version
#*  
#*****************************************************************************
#
#
#  $m => Terminal adapter type (Emtpy = Unknown)
  let $m=""
  let $d=$env("GPRS_DEVICE")
  if len($d)=0 let $d="/dev/com1"
  open com $d
#
  let $b=$env("GPRS_BAUDRATE")
  if len($b)=0 let $b="115200"
  print "Starting GPRS connection on device ",$d," (",$b,"baud)^j"
  set com $b+"n81"
  set senddelay 0.05
#
# Test if device is connected
#
:device_detect
  flash 0.1
  send "ATi^m"
  waitfor 5 "OK" "SIEMENS"
  if % = -1 goto device_hangup
  if % = 0 goto start
  if % = 1 goto device_siemens
#
:device_hangup
  sleep 1
  send "+++"
  sleep 1
  send "AT^m"
  waitfor 5 "OK"
  if % = 0 goto device_detect
#
  print "Terminal adapter not responding, check if device is powered on and connected properly.","^j"
  sleep 5
  goto err_exit
#
:device_siemens
  waitfor 5 "MC35" "HC25"
  if % = 0 let $m="MC35"
  if % = 1 let $m="HC25"

#
#
#
:start
  if $m="MC35"  print "Found Siemens MC35 GPRS terminal adapter^j"
  if $m="HC25"  print "Found Siemens HC25 UMTS/GPRS terminal adapter^j"
  if len($m)=0  print "Found unknown terminal adapter^j"
  flash 0.1
  send "AT+CPIN?^m"
  waitfor 30 "SIM PUK","SIM PIN","READY","ERROR","ERR"
  if % = -1 goto error
  if % = 0 goto ready
  if % = 1 goto getpin
  if % = 2 goto ready
  if % = 3 goto error
  if % = 4 goto error
:error
  print $s," ***SIM ERROR***","^j"
  goto err_exit
#
:getpin
  let $x=$env("GPRS_PIN")
  let a=len($x)
  if a=0 goto pinenverr
  if a<>4 goto pinerror
  let c=0
:test
  let $c=$mid($x,c,1)
  if $c<"0" goto pinerror
  if $c>"9" goto pinerror
  inc c
  if c<4 goto test
  let a=val($x)
  if a<0 goto pinerror
  if a>9999 goto pinerror
  let $c=$left($x,4)
:enterpin
  send "AT+CPIN=\""
  send $c
  send "\"^m"
  waitfor 20 "OK","ERR"
  if % = -1 goto timeerror
  if % = 0 goto ready
  if % = 1 goto pinerror
:pinenverr
  print "ERROR: The GPRS_PIN env variable is not set","^j"
  goto err_exit
:pinerror
  print "ERROR: PIN code must be 4 decimal digits only","^j"
  print "Caution! - entering the wrong PIN code three times will lock the SIM","^j"
  goto err_exit
:timeerror
  print "ERROR: timeout, device did not respond to PIN command entry.","^j"
  goto err_exit
:ready
  print "SIM ready","^j"
:cont
  print "Waiting for Registration..(120 sec max)"
  let c = 0
:waitreg
  send "AT+CREG?^m"
  waitfor 2 "OK" "+CREG: 0,1","+CREG: 0,5"
  if % = -1 goto reg_noresponse
  if % = 0 goto regagain
  if % = 1 goto homereg
  if % = 2 goto roamreg
:regagain
  if c > 120 goto regtimeout
  let c=c+2
  print "."
  goto waitreg
:reg_noresponse
  print "^jError: No response from terminal adapter on AT+CREG?","^j"
  goto err_exit
:regtimeout
  print "^jFailed to register","^j"
  goto err_exit
:homereg
  print "^j"
  let $n="Registered on home network: "
  goto registered
:roamreg
  print "^j"
  let $n="Registered on roaming network: "
  goto registered
:registered
  let $o=$env("GPRS_OPERATOR")
  if len($o)=0 goto oper_OK
  print "Setting manual selected operator to ",$o,"^j"
  waitquiet 1 0.1
  send "AT+COPS=1,2,\""+$o+"\"^m"
  waitfor 20 "OK","ERR"
  if % = 0 goto oper_OK
:oper_error
  print "ERROR setting manual operator","^j"
  goto err_exit
:oper_OK
# reset operator format to alphanumeric 16 characters
  send "AT+COPS=3,0^m"
  waitfor 20 "OK","ERR"
# Query current operator
  waitquiet 1 0.1
  send "AT+COPS?^m"
  get 2 "^m" $s
  get 2 "^m" $s
  let a=len($s)
  let b=a-12
  if b < 1 goto regtimeout
  let $c=$right($s,b)
  print $n,$c,"^j"
  let c=0
:signal
  waitquiet 1 0.1
  send "AT+CSQ^m"
  get 2 "^m" $s
  get 2 "^m" $s
  let a=len($s)
  let a=a-6
  let $s=$right($s,a)
  if $s <> "0,0" goto sigcont
  if c > 3 goto sigexit
  let c=c+1
  pause 1
  goto signal
:sigexit
  print "Signal strength measure 0,0 too low!^j"
  goto err_exit
:sigcont
  print "Signal Quality:",$s,"^j"
  waitquiet 1 0.1
#
# SET APN
#
:getapn
  let $x=$env("GPRS_APN")
  let a=len($x)
  if a=0 goto apn_error
  if a>32 goto apn_error
:enterapn
  print "Entering APN: ",$x,"^j"
  send "AT+CGDCONT=1,\"IP\",\""+$x+"\"^m"
  waitfor 20 "OK","ERR"
  if % = -1 goto apn_timeerror
  if % = 0 goto apn_OK
  if % = 1 goto apn_error
:apn_error
  print "ERROR entering APN","^j"
  print "The GPRS_APN env variable is not set","^j"
  exit 1
:apn_timeerror
  print "ERROR entering APN","^j"
  print "The device timeout.","^j"
  goto err_exit
:apn_OK
#
# Dial GPRS
#
  print "Dialing","^j"
  send "ATD *99**PPP*1#^m"
  waitfor 90 "CONNECT","ERR"
  if % = -1 goto dial_timeerror
  if % = 0 goto dial_CONNECT
  if % = 1 goto dial_error
:dial_timeerror
  print "Dialing Timout","^j"
  goto err_exit
:dial_error
  print "Error calling GPRS","^j"
  goto err_exit
:dial_CONNECT
  print "Connected, starting pppd","^j"
  let $u=$env("GPRS_USER")
  if len($u)>0 let $u="user "+$u
  let $o=$env("GPRS_PPP_OPTIONS")
  let $o="call gprs_comgt nolog nodetach "+$o+" "+$d+" "+$b+" "+$u
  print "ppp command line: ","/usr/sbin/pppd  "+$o,"^j"
  system "/usr/sbin/pppd "+$o
  print "pppd terminated","^j"
  system "fuser -k "+$d
  print "ready","^j"
#exit successfully
  exit 0
#
#
#
#
#
:err_exit
#
# in case of an error send reset command to device
  print "Reseting terminal adapter^j"
  send "AT+CFUN=1,1^m"
  exit 1
